# Lecture 9 (2022-10-18) Slides week 5

## Modules in Haskell

Using modules to structure a large program has a number of advantages:

- Parts of the system can be built separately from each other.
- Parts of the system can be compiled separately.
- Libraries of components can be reused, by importing the appropriate module containing them.

```Haskell
module Ant where

type Ants  = ...
antEater x = ...
```

The convention for file names is that a module Ant resides in the Haskell file `Ant.hs` or `Ant.lhs`.

## Importing a module

```Haskell
module Bee where

import Ant

beeKeeper = ...
```

Importing the module `Ant` means that the **visible** definitions from the module can be used in `Bee`. By default the visible definitions in a module are those which appear in the module itself.

```Haskell
module Cow where

import Bee
```

The definitions of `Ant` are **not** visible in `Cow`.

## Export Control

We can control what is exported by following the name of the module with a list of what is to be exported.

```Haskell
-- 1
module Bee (beeKeeper, Ants, antEater) where ...

-- 2
module Bee (beeKeeper, module Ant) where ...

-- 3
module Fish where
    type Fish = (String,Size)

-- 4
module Fish (Fish(..), ...) where
    newtype Fish = F (String,Size)

-- 5
module Fish (Fish, ...) where
    newtype Fish = F (String,Size)
```

## Import Control

Examples:

```Haskell
import Ant (Ants)

import Ant hiding (antEater)

module Bear where
    import qualified Ant
    antEater x = … Ant.antEater x …

import Insect as Ant

import Prelude hiding (words)

import qualified Prelude
```

## Overloading and type classes

A polymorphic function such as `length` has a single definition which works over all its types.

```Haskell
length	:: [a] -> Int
length = foldl' (\n _ -> n + 1) 0
```

An overloaded function like equality `(==)`, `+` and `show` can be used at a variety of types, but with different definitions being used at different types.

## Why Overloading?

```Haskell
elemBool :: Bool -> [Bool] -> Bool
elemBool x []       = False
elemBool x (y:ys)   = (x ==Bool y) || elemBool x ys

elemInt :: Int -> [Int] -> Bool
elemInt x []        = False
elemInt x (y:ys)    = (x ==Int y) || elemInt x ys
```

Generalization may lead to the definition:

```Haskell
elemGen :: (a -> a -> Bool) -> a -> [a] -> Bool
elemGen p x []      = False
elemGen p x (y:ys)  = p x y || elemGen x ys
```

but this is too general in a sense, because it can be used with **any** parameter of type `a -> a -> Bool` rather than just an equality check.

Generalization in the following way will not work. The definition

```Haskell
elem :: a -> [a] -> Bool
elem x []	= False
elem x (y:ys)	= (x == y) || elem x ys
```

will cause an error

```Text
No instance for (Eq a)
        arising from a use of `=='
In the first argument of `(||)', namely `x == y'
In the expression: x == y || elem x ys
In an equation for `elem`: elem x (y : ys) = x == y || elem x ys
```

because this definition requires that `(==) :: a -> a -> Bool` is already defined.

## Classes

A type class or simply a `class` defines a collection of types over which specific functions are defined.

```Haskell
class Eq a where
    (==) :: a -> a -> Bool
```

Members of a `class` are called instances. Built-in instances of `Eq` include the base types `Int`, `Float`, `Bool`, `Char`, tuples and lists built from types which are themselves instances of `Eq`, e.g., `(Int,Bool)` and `[[Char]]`.

```Haskell
elem :: Eq a => a -> [a] -> Bool
elem x []       = False
elem x (y:ys)   = (x == y) || elem x ys
```

`(+1) :: Int -> Int`

`elem (+1) []` causes an error

```Text
No instance for (Eq (a0 -> a0))
    arising from a use of `elem'
Possible fix: add an instance declaration for (Eq (a0 -> a0))
In the expression: elem (+ 1) []
In an equation for `it': it = elem (+ 1) []
```

which conveys the fact that `Int -> Int` is not an instance of the `Eq` class.

## Instances of Classes

Examples:

```Haskell
instance Eq Bool where
    True  == True  = True
    False == False = True
    _     == _     = False

instance Eq a => Eq [a] where
    []     == []     =  True
    (x:xs) == (y:ys) =  x==y && xs==ys
    _      == _      =  False

instance (Eq a, Eq b) => Eq (a,b) where
    (x,y) == (z,w) = x==z && y==w
```

## Default Definitions

The Haskell `Eq` class is in fact defined by

```Haskell
class Eq a where
    (==), (/=) :: a -> a -> Bool
    x /= y  = not (x==y)
    x == y  = not (x/=y)
```

Both functions have default definitions in terms of the other function. At any instance a definition of at least one of == and /= needs to be provided.

## Derived Classes

To be ordered, a type must carry operations `>`, `>=` and so on, as well as the equality operations.

```Haskell
class (Eq a) => Ord a where
    compare                :: a -> a -> Ordering
    (<), (<=), (>=), (>)   :: a -> a -> Bool
    max, min               :: a -> a -> a
```

## Example

```Haskell
class Visible a where
    toString    :: a -> String
    size        :: a -> Int

instance Visible Bool where
    toString True   = ”True”
    toString False  = ”False”
    size _          = 1

instance Visible a => Visible [a] where
    toString    = concat . map toString
    size        = foldr (+) 1 . map size

sort :: Ord a => [a] -> [a]

vSort :: (Ord a, Visible a) => [a] -> String
vSort = toString . sort

class (Ord a, Visible a) => OrdVis a

vSort' :: OrdVis a => [a] -> String
vSort' = toString . sort
```

## A Tour of the Built-in Haskell Classes

Many of the Haskell built-in classes are numeric, and are built to deal with overloading of the numerical operations. We will not study those classes.

Class `Eq`:

```Haskell
class Eq a where
    (==), (/=) :: a -> a -> Bool
    x /= y  = not (x==y)
    x == y  = not (x/=y)
```

Instances: All except of `IO`, `->`

## Class `Ord`

```Haskell
class (Eq a) => Ord a where
    compare                 :: a -> a -> Ordering
    (<), (<=), (>=), (>)    :: a -> a -> Bool
    max, min                :: a -> a -> a
-- Minimal complete definition: (<=) or compare
-- using compare can be more efficient for complex types
    compare x y | x==y      = EQ
                | x<=y      = LT
                | otherwise = GT
    x <= y                  = compare x y /= GT
    x <  y                  = compare x y == LT
    x >= y                  = compare x y /= LT
    x >  y                  = compare x y == GT
    max x y     | x <= y    = y
                | otherwise = x
    min x y     | x <= y    = x
                | otherwise = y
```

Instances: All except `IO`, `IOError`, `->`

## Class `Enum`

```Haskell
class Enum a where
    succ, pred          :: a -> a
    toEnum              :: Int -> a
    fromEnum            :: a -> Int
    enumFrom            :: a -> [a]             -- [n..]
    enumFromThen        :: a -> a -> [a]        -- [n,m..]
    enumFromTo          :: a -> a -> [a]        -- [n..m]
    enumFromThenTo      :: a -> a -> a -> [a]   -- [n,n'..m]

    -- Minimal complete definition: toEnum, fromEnum
    succ                    = toEnum . (1+)       . fromEnum
    pred                    = toEnum . subtract 1 . fromEnum
    enumFrom x              = map toEnum [ fromEnum x ..]
    enumFromTo x y          = map toEnum [ fromEnum x .. fromEnum y ]
    enumFromThen x y        = map toEnum [ fromEnum x, fromEnum y ..]
    enumFromThenTo x y z    = map toEnum [ fromEnum x, fromEnum y .. fromEnum z ]
```

Instances: `()`, `Bool`, `Char`, `Int`, `Integer`, `Float` `Double`

## Class `Bounded`

```Haskell
class Bounded a where
    minBound, maxBound :: a
    -- Minimal complete definition: All
```

Instances: `Int`, `Char`, `Bool` (but not `Integer`)
